plugins {
	id "org.springframework.boot" version "2.2.6.RELEASE"
	id "io.spring.dependency-management" version "1.0.9.RELEASE"
	id 'org.jetbrains.kotlin.jvm' version '1.3.71'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.3.71'
	id("nu.studer.jooq") version "4.2"
}

group = "com.example"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "Hoxton.SR4")
}

sourceSets {
	main.kotlin.srcDirs += 'src/main/kotlin'
	main.kotlin.srcDirs += 'src/main/generated-jooq'
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.boot:spring-boot-starter-jooq"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
	implementation "org.jetbrains.kotlin:kotlin-reflect"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	implementation "org.springframework.cloud:spring-cloud-starter-sleuth"
	implementation "org.springframework.cloud:spring-cloud-stream"
	runtimeOnly "mysql:mysql-connector-java:8.0.15"

	implementation "org.glassfish.jaxb:jaxb-core:2.3.0.1"
	implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"

	implementation "org.jooq:jooq"
	implementation "org.jooq:jooq-parent"
	implementation "org.jooq:jooq-meta"
	implementation "org.jooq:jooq-codegen"
	implementation "org.jooq:jooq-meta-extensions"
	jooqRuntime "mysql:mysql-connector-java:8.0.15"
	jooqRuntime "javax.activation:activation:1.1.1"
	jooqRuntime "javax.xml.bind:jaxb-api:2.3.0"
	jooqRuntime "com.sun.xml.bind:jaxb-core:2.3.0.1"
	jooqRuntime "com.sun.xml.bind:jaxb-impl:2.3.0.1"
	jooqRuntime "org.glassfish.jaxb:jaxb-core:2.3.0.1"
	jooqRuntime "org.glassfish.jaxb:jaxb-runtime:2.3.2"

	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude group: "org.junit.vintage", module: "junit-vintage-engine"
	}
	testImplementation "org.springframework.cloud:spring-cloud-stream-test-support"
}

dependencyManagement {
	imports {
		mavenBom("org.springframework.cloud:spring-cloud-dependencies:${property("springCloudVersion")}")
	}
}

jooq {
	version = "3.13.1"
	edition = "OSS"
	inventory(sourceSets.main) {
		jdbc {
			driver = "com.mysql.cj.jdbc.Driver"
			url = "jdbc:mysql://127.0.0.1:3306/inventory?nullNamePatternMatchesAll=true&createDatabaseIfNotExist=true&useSSL=false"
			user = "root"
			password = "password"
		}
		generator {
			name = "org.jooq.codegen.DefaultGenerator"
			database {
				name = "org.jooq.meta.mysql.MySQLDatabase"
				inputSchema = "inventory"
				forcedTypes {
					forcedType {
						name = 'BOOLEAN'
						includeExpression = "(?i:TINYINT\\(1\\))"
						includeTypes = ".*"
					}
				}
				outputSchemaToDefault = true
			}
			generate {
				javaTimeTypes = true
			}
			target {
				packageName = "inventory.generated.jooq"
				directory = "src/main/generated-jooq/"
			}
		}
	}
}



test {
	useJUnitPlatform()
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
  	}
}

bootRun {
	main = "com.example.inventory.InventoryApplicationKt"
	args("--spring.profiles.active=demo")
}

